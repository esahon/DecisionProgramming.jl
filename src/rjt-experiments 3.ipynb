{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Logging\n",
    "using JuMP, Gurobi\n",
    "using DecisionProgramming"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "const N = 4\n",
    "\n",
    "diagram = InfluenceDiagram()\n",
    "\n",
    "add_node!(diagram, ChanceNode(\"H1\", [], [\"ill\", \"healthy\"]))\n",
    "for i in 1:N-1\n",
    "    # Testing result\n",
    "    add_node!(diagram, ChanceNode(\"T$i\", [\"H$i\"], [\"positive\", \"negative\"]))\n",
    "    # Decision to treat\n",
    "    add_node!(diagram, DecisionNode(\"D$i\", [\"T$i\"], [\"treat\", \"pass\"]))\n",
    "    # Cost of treatment\n",
    "    add_node!(diagram, ValueNode(\"C$i\", [\"D$i\"]))\n",
    "    # Health of next period\n",
    "    add_node!(diagram, ChanceNode(\"H$(i+1)\", [\"H$(i)\", \"D$(i)\"], [\"ill\", \"healthy\"]))\n",
    "end\n",
    "add_node!(diagram, ValueNode(\"MP\", [\"H$N\"]))\n",
    "\n",
    "generate_arcs!(diagram);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Add probabilities for node H1\n",
    "add_probabilities!(diagram, \"H1\", [0.1, 0.9])\n",
    "\n",
    "# Declare proability matrix for health nodes H_2, ... H_N-1, which have identical information sets and states\n",
    "X_H = ProbabilityMatrix(diagram, \"H2\")\n",
    "X_H[\"healthy\", \"pass\", :] = [0.2, 0.8]\n",
    "X_H[\"healthy\", \"treat\", :] = [0.1, 0.9]\n",
    "X_H[\"ill\", \"pass\", :] = [0.9, 0.1]\n",
    "X_H[\"ill\", \"treat\", :] = [0.5, 0.5]\n",
    "\n",
    "# Declare proability matrix for test result nodes T_1...T_N\n",
    "X_T = ProbabilityMatrix(diagram, \"T1\")\n",
    "X_T[\"ill\", \"positive\"] = 0.8\n",
    "X_T[\"ill\", \"negative\"] = 0.2\n",
    "X_T[\"healthy\", \"negative\"] = 0.9\n",
    "X_T[\"healthy\", \"positive\"] = 0.1\n",
    "\n",
    "for i in 1:N-1\n",
    "    add_probabilities!(diagram, \"T$i\", X_T)\n",
    "    add_probabilities!(diagram, \"H$(i+1)\", X_H)\n",
    "end\n",
    "\n",
    "for i in 1:N-1\n",
    "    add_utilities!(diagram, \"C$i\", [-100.0, 0.0])\n",
    "end\n",
    "\n",
    "add_utilities!(diagram, \"MP\", [300.0, 1000.0])\n",
    "\n",
    "generate_diagram!(diagram);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "ID_to_RJT (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# Implements Algorithm 1 from Parmentier et al. (2020)\n",
    "function ID_to_RJT(diagram)\n",
    "    C_rjt = Dict{String, Vector{String}}()\n",
    "    A_rjt = []\n",
    "    namelist = [node.name for node in diagram.Nodes]\n",
    "    for j in length(diagram.Nodes):-1:1\n",
    "        C_j = copy(diagram.Nodes[j].I_j)\n",
    "        push!(C_j, namelist[j])\n",
    "        for a in A_rjt \n",
    "            if a[1] == namelist[j]\n",
    "                push!(C_j, setdiff(C_rjt[a[2]], [a[2]])...)\n",
    "            end\n",
    "        end\n",
    "        C_j = unique(C_j)\n",
    "        C_j_aux = sort([(elem, findfirst(isequal(elem), namelist)) for elem in C_j], by = last)\n",
    "        C_j = [C_j_tuple[1] for C_j_tuple in C_j_aux]\n",
    "        C_rjt[namelist[j]] = C_j\n",
    "        \n",
    "        if length(C_rjt[namelist[j]]) > 1\n",
    "            u = maximum([findfirst(isequal(name), namelist) for name in setdiff(C_j, [namelist[j]])])\n",
    "            push!(A_rjt, (namelist[u], namelist[j]))\n",
    "        end\n",
    "    end\n",
    "    return C_rjt, A_rjt\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "cluster_variables_and_constraints (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# Using the influence diagram and decision variables z from DecisionProgramming.jl,  \n",
    "# adds the variables and constraints of the corresponding RJT model\n",
    "function cluster_variables_and_constraints(model, diagram, z)\n",
    "\n",
    "    # I would prefer that we made some minor modifications to the diagram structure, \n",
    "    # having these as dictionaries makes things a lot easier in the model formulation\n",
    "    S = Dict{String, Vector{String}}()\n",
    "    idx = 1\n",
    "    for node in diagram.Nodes\n",
    "        if !isa(node, ValueNode)\n",
    "            S[node.name] = node.states\n",
    "            idx+=1\n",
    "        end\n",
    "    end    \n",
    "\n",
    "    I_j = Dict{String, Vector{String}}()\n",
    "    for (idx, name) in enumerate(diagram.Names)\n",
    "        I_j[name] = diagram.Names[diagram.I_j[idx]]\n",
    "    end\n",
    "\n",
    "    Nodes = Dict{String, AbstractNode}()\n",
    "    for node in diagram.Nodes\n",
    "        Nodes[node.name] = node\n",
    "    end\n",
    "\n",
    "    States = Dict{String, Vector{String}}()\n",
    "    for (idx, name) in enumerate(diagram.Names)\n",
    "        if !isa(Nodes[name], ValueNode)\n",
    "            States[name] = diagram.States[idx]\n",
    "        else\n",
    "            States[name] = []\n",
    "        end\n",
    "    end\n",
    "\n",
    "    X = Dict{String, Probabilities}()\n",
    "    idx = 1\n",
    "    for name in diagram.Names\n",
    "        if isa(Nodes[name], ChanceNode)\n",
    "            X[name] = diagram.X[idx]\n",
    "            idx += 1\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    Y = Dict{String, Utilities}()\n",
    "    idx = 1\n",
    "    for name in diagram.Names\n",
    "        if isa(Nodes[name], ValueNode)\n",
    "            Y[name] = diagram.Y[idx]\n",
    "            idx += 1\n",
    "        end\n",
    "    end\n",
    "\n",
    "    z_dict = Dict{String, Array{VariableRef}}()\n",
    "    idx = 1\n",
    "    for name in diagram.Names\n",
    "        if isa(Nodes[name], DecisionNode)\n",
    "            z_dict[name] = z.z[idx]\n",
    "            idx += 1\n",
    "        end\n",
    "    end\n",
    "\n",
    "\n",
    "    # Get the RJT structure\n",
    "    C_rjt, A_rjt = ID_to_RJT(diagram)\n",
    "\n",
    "    # Variables corresponding to the nodes in the RJT\n",
    "    μ = Dict{String, Array{VariableRef}}()\n",
    "    for j in keys(C_rjt)\n",
    "        if !isa(Nodes[j], ValueNode)\n",
    "            μ[j] = Array{VariableRef}(undef, Tuple(length.([getindex.(Ref(S), C_rjt[j])]...)))\n",
    "            for index in CartesianIndices(μ[j])\n",
    "                μ[j][index] = @variable(model, base_name=\"μ_$j($(join(Tuple(index),',')))\", lower_bound=0)\n",
    "            end\n",
    "            # Probability distributions μ sum to 1\n",
    "            @constraint(model, sum(μ[j]) == 1)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    for a in A_rjt\n",
    "        if !isa(Nodes[a[2]], ValueNode)\n",
    "            intersection = C_rjt[a[1]] ∩ C_rjt[a[2]]\n",
    "            C1_minus_C2 = Tuple(setdiff(collect(1:length(C_rjt[a[1]])), indexin(intersection, C_rjt[a[1]])))\n",
    "            C2_minus_C1 = Tuple(setdiff(collect(1:length(C_rjt[a[2]])), indexin(intersection, C_rjt[a[2]])))\n",
    "            @constraint(model, \n",
    "                dropdims(sum(μ[a[1]], dims=C1_minus_C2), dims=C1_minus_C2) .== \n",
    "                dropdims(sum(μ[a[2]], dims=C2_minus_C1), dims=C2_minus_C1))\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Variables μ_{\\breve{C}_v} = ∑_{x_v} μ_{C_v}\n",
    "    μ_breve = Dict{String, Array{VariableRef}}()\n",
    "    for j in keys(C_rjt)\n",
    "        if !isa(Nodes[j], ValueNode)\n",
    "            μ_breve[j] = Array{VariableRef}(undef, Tuple(length.([getindex.(Ref(S), setdiff(C_rjt[j], [j]))]...)))\n",
    "            for index in CartesianIndices(μ_breve[j])\n",
    "                # Moments μ_{\\breve{C}_v} (the moments from above, but with the last variable dropped out)\n",
    "                μ_breve[j][index] = @variable(model, base_name=\"μ_breve_$j($(join(Tuple(index),',')))\", lower_bound=0)\n",
    "                # μ_{\\breve{C}_v} = ∑_{x_v} μ_{C_v}\n",
    "                @constraint(model, μ_breve[j][index] .== dropdims(sum(μ[j], dims=findfirst(isequal(j), C_rjt[j])), dims=findfirst(isequal(j), C_rjt[j]))[index])\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Add in the conditional probabilities and decision strategies\n",
    "    for name in diagram.Names \n",
    "        if !isa(Nodes[name], ValueNode) # In our structure, value nodes are not stochastic and the whole objective thing doesn't really work in this context\n",
    "            I_j_mapping = [findfirst(isequal(node), C_rjt[name]) for node in I_j[name]] # Map the information set to the variables in the cluster\n",
    "            for index in CartesianIndices(μ_breve[name])\n",
    "                for s_j in 1:length(States[name])\n",
    "                    if isa(Nodes[name], ChanceNode)\n",
    "                        # μ_{C_v} = p*μ_{\\breve{C}_v}\n",
    "                        @constraint(model, μ[name][Tuple(index)...,s_j] == X[name][Tuple(index)[I_j_mapping]...,s_j]*μ_breve[name][index])\n",
    "                    elseif isa(Nodes[name], DecisionNode)\n",
    "                        # μ_{C_v} ≤ z\n",
    "                        @constraint(model, μ[name][Tuple(index)...,s_j] <= z_dict[name][Tuple(index)[I_j_mapping]...,s_j])\n",
    "                    end\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Build the objective. The key observation here is that the information set\n",
    "    # of a value node is always included in the previous cluster by construction\n",
    "    @objective(model, Max, 0)\n",
    "    for j in keys(C_rjt)\n",
    "        if isa(Nodes[j], ValueNode)\n",
    "            i = A_rjt[findfirst(a -> a[2] == j, A_rjt)][1]\n",
    "            I_j_mapping = [findfirst(isequal(node), C_rjt[i]) for node in I_j[j]]\n",
    "            for index in CartesianIndices(μ[i])\n",
    "                set_objective_coefficient(model, μ[i][index], Y[j][Tuple(index)[I_j_mapping]...])\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return μ\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Set parameter Username\n",
      "Academic license - for non-commercial use only - expires 2025-02-14\n",
      "Set parameter IntFeasTol to value 1e-09\n",
      "Set parameter TimeLimit to value 3600\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "A JuMP Model\n",
       "Maximization problem with:\n",
       "Variables: 105\n",
       "Objective function type: AffExpr\n",
       "`AffExpr`-in-`MathOptInterface.EqualTo{Float64}`: 115 constraints\n",
       "`AffExpr`-in-`MathOptInterface.LessThan{Float64}`: 24 constraints\n",
       "`VariableRef`-in-`MathOptInterface.GreaterThan{Float64}`: 93 constraints\n",
       "`VariableRef`-in-`MathOptInterface.ZeroOne`: 12 constraints\n",
       "Model mode: AUTOMATIC\n",
       "CachingOptimizer state: EMPTY_OPTIMIZER\n",
       "Solver name: Gurobi"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "model = Model()\n",
    "# set_silent(model)\n",
    "optimizer = optimizer_with_attributes(\n",
    "    () -> Gurobi.Optimizer(Gurobi.Env()),\n",
    "    \"IntFeasTol\"      => 1e-9,\n",
    "    \"TimeLimit\"       => 3600,\n",
    "    # \"DualReductions\"  => 0,\n",
    ")\n",
    "set_optimizer(model, optimizer)\n",
    "\n",
    "z = DecisionVariables(model, diagram)\n",
    "\n",
    "μ = cluster_variables_and_constraints(model, diagram, z)\n",
    "\n",
    "model"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Set parameter IntFeasTol to value 1e-09\n",
      "Set parameter TimeLimit to value 3600\n",
      "Gurobi Optimizer version 11.0.1 build v11.0.1rc0 (win64 - Windows 10.0 (19045.2))\n",
      "\n",
      "CPU model: 11th Gen Intel(R) Core(TM) i5-1135G7 @ 2.40GHz, instruction set [SSE2|AVX|AVX2|AVX512]\n",
      "Thread count: 4 physical cores, 8 logical processors, using up to 8 threads\n",
      "\n",
      "Optimize a model with 139 rows, 105 columns and 405 nonzeros\n",
      "Model fingerprint: 0xef404c3d\n",
      "Variable types: 93 continuous, 12 integer (12 binary)\n",
      "Coefficient statistics:\n",
      "  Matrix range     [1e-01, 1e+00]\n",
      "  Objective range  [1e+02, 1e+03]\n",
      "  Bounds range     [0e+00, 0e+00]\n",
      "  RHS range        [1e+00, 1e+00]\n",
      "Presolve removed 110 rows and 81 columns\n",
      "Presolve time: 0.01s\n",
      "Presolved: 29 rows, 24 columns, 89 nonzeros\n",
      "Variable types: 17 continuous, 7 integer (6 binary)\n",
      "Found heuristic solution: objective 669.3900000\n",
      "\n",
      "Root relaxation: objective 7.305380e+02, 14 iterations, 0.01 seconds (0.00 work units)\n",
      "\n",
      "    Nodes    |    Current Node    |     Objective Bounds      |     Work\n",
      " Expl Unexpl |  Obj  Depth IntInf | Incumbent    BestBd   Gap | It/Node Time\n",
      "\n",
      "     0     0  730.53804    0    3  669.39000  730.53804  9.13%     -    0s\n",
      "H    0     0                     726.8121000  730.53804  0.51%     -    0s\n",
      "\n",
      "Explored 1 nodes (14 simplex iterations) in 0.03 seconds (0.00 work units)\n",
      "Thread count was 8 (of 8 available processors)\n",
      "\n",
      "Solution count 2: 726.812 669.39 \n",
      "\n",
      "Optimal solution found (tolerance 1.00e-04)\n",
      "Best objective 7.268121000000e+02, best bound 7.268121000000e+02, gap 0.0000%\n",
      "\n",
      "User-callback calls 516, time in user-callback 0.01 sec\n"
     ]
    }
   ],
   "source": [
    "optimize!(model)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [],
   "source": [
    "Z = DecisionStrategy(z)\n",
    "# S_probabilities = StateProbabilities(diagram, Z)\n",
    "U_distribution = UtilityDistribution(diagram, Z);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "┌─────────────┬─────────────┐\n",
      "│\u001b[1m     Utility \u001b[0m│\u001b[1m Probability \u001b[0m│\n",
      "│\u001b[90m     Float64 \u001b[0m│\u001b[90m     Float64 \u001b[0m│\n",
      "├─────────────┼─────────────┤\n",
      "│  100.000000 │    0.047857 │\n",
      "│  200.000000 │    0.129330 │\n",
      "│  300.000000 │    0.127980 │\n",
      "│  800.000000 │    0.061753 │\n",
      "│  900.000000 │    0.247160 │\n",
      "│ 1000.000000 │    0.385920 │\n",
      "└─────────────┴─────────────┘\n",
      "┌──────────┬────────────┐\n",
      "│\u001b[1m     Name \u001b[0m│\u001b[1m Statistics \u001b[0m│\n",
      "│\u001b[90m   String \u001b[0m│\u001b[90m    Float64 \u001b[0m│\n",
      "├──────────┼────────────┤\n",
      "│     Mean │ 726.812100 │\n",
      "│      Std │ 338.460723 │\n",
      "│ Skewness │  -0.811628 │\n",
      "│ Kurtosis │  -1.173465 │\n",
      "└──────────┴────────────┘\n"
     ]
    }
   ],
   "source": [
    "# print_decision_strategy(diagram, Z, S_probabilities)\n",
    "\n",
    "print_utility_distribution(U_distribution)\n",
    "\n",
    "print_statistics(U_distribution)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.2",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
